plugins {
    id 'java-library'
    id 'io.github.gradle-nexus.publish-plugin' version '2.0.0'
    id 'maven-publish'
    id 'signing'
}

repositories {
    mavenCentral()
}

dependencies {
    api libs.org.slf4j.slf4j.api
    testImplementation libs.org.junit.jupiter.junit.jupiter.api
    testImplementation libs.org.junit.jupiter.junit.jupiter.engine
    testImplementation libs.org.junit.jupiter.junit.jupiter
    testImplementation libs.org.slf4j.slf4j.simple
}

group = 'org.purejava'
version = '1.4.3-SNAPSHOT'
description = 'Java bindings for libappindicator-gtk3 in 100% pure Java'
java.sourceCompatibility = JavaVersion.VERSION_22

def sonatypeUsername = System.getenv("SONATYPE_USERNAME") ?: ''
def sonatypePassword = System.getenv("SONATYPE_PASSWORD") ?: ''

java {
    withSourcesJar()
    withJavadocJar()
}

test {
    useJUnitPlatform()
    filter {
        includeTestsMatching "AppIndicatorTest"
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from(components.java)
            pom {
                name = 'libappindicator-gtk3-java-full'
                description = 'Java bindings for libappindicator-gtk3 in 100% pure Java'
                url = 'https://github.com/purejava/appindicator-gtk3-java'
                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id = 'purejava'
                        name = 'Ralph Plawetzki'
                        email = 'ralph@purejava.org'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/purejava/appindicator-gtk3-java.git'
                    developerConnection = 'scm:git:ssh://github.com/purejava/appindicator-gtk3-java.git'
                    url = 'https://github.com/purejava/appindicator-gtk3-java/tree/develop'
                }
                issueManagement {
                    system = 'GitHub Issues'
                    url = 'https://github.com/purejava/appindicator-gtk3-java/issues'
                }
            }
        }
    }
}

nexusPublishing {
    repositories {
        sonatype {  //only for users registered in Sonatype after 24 Feb 2021
            nexusUrl.set(uri("https://s01.oss.sonatype.org/service/local/"))
            snapshotRepositoryUrl.set(uri("https://s01.oss.sonatype.org/content/repositories/snapshots/"))
            username = sonatypeUsername
            password = sonatypePassword
        }
    }
}

if (!version.toString().endsWith("-SNAPSHOT")) {
    signing {
        useGpgCmd()
        sign configurations.runtimeElements
        sign publishing.publications.mavenJava
    }
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

tasks.withType(Javadoc) {
    failOnError = false
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.withType(Javadoc) {
    options.encoding = 'UTF-8'
}
